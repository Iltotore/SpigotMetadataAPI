plugins {
    id 'java'
    id 'maven'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id 'maven-publish'
    id 'fr.il_totore.manadrop' version '0.3-SNAPSHOT-fixed'
}



project.group 'fr.il_totore'
project.version '4.0-SNAPSHOT'

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    project.targetCompatibility = 1.8
    project.sourceCompatibility = 1.8

    compileJava {
        targetCompatibility = 1.8
        sourceCompatibility = 1.8
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }
}

dependencies {

    compile project(nms('1_8_R3'))
    compile project(nms('1_9_R1'))
    compile project(nms('1_9_R2'))
    compile project(nms('1_10_R1'))
    compile project(nms('1_11_R1'))
    compile project(nms('1_12_R1'))
    compile project(nms('1_13_R1'))
    compile project(nms('1_13_R2'))
    compile project(nms('1_14_R1'))
    compile project(nms('1_15_R1'))
    compile project(nms('1_16_R1'))

    compile project(':SpigotMetadataAPI:Abstract')
    compile project(':SpigotMetadataAPI:Core')
}

static String nms(String version) {
    return ':SpigotMetadataVersion:SpigotMetadataV' + version
}

shadowJar {
    configurations = [project.configurations.compile]
}

def documentedProjects = [
        ':SpigotMetadataAPI:Core',
        ':SpigotMetadataAPI:Abstract'
]

task alljavadoc(type: Javadoc) {
    source documentedProjects.collect { name -> project(name).sourceSets.main.allJava }
    classpath = files(documentedProjects.collect { name -> project(name).sourceSets.main.compileClasspath })
    destinationDir = file("${buildDir}/docs/javadoc")
    group 'documentation'
}

publishing {
    publications {
        compiler(MavenPublication) {
            groupId group
            artifactId 'spigotmetadataapi'
            version version
            artifact new File(buildDir, 'libs/SpigotMetadata-' + version + '-all.jar')
        }
    }
}

buildTools {
    versions '1.8.8', '1.9', '1.9.4', '1.10.2', '1.11', '1.12.2', '1.13', '1.13.2', '1.14.4', '1.15', '1.16.1', '1.16.2'
    workDir = file("work/")
}